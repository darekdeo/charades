apply plugin: 'com.android.application'
apply plugin: 'com.mutualmobile.gradle.plugins.dexinfo'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion '26'

    applicationVariants.all { variant ->
        def productFlavor = variant.productFlavors[0] != null ? "${variant.productFlavors[0].name.capitalize()}" : ""
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks["compile${productFlavor}${buildType}UnitTestSources"].dependsOn(tasks["merge${productFlavor}${buildType}Assets"])
    }

    defaultConfig {
        applicationId "pl.dariuszdeoniziak.charades"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "0.1"
        multiDexEnabled false
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def supportLibVersion = '25.3.1'
def rxLibVersion = '2.0.1'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile group: 'com.google.guava', name: 'guava', version: '20.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.3.1"

    // newer versions are not compatible with robolectric:3.1.4
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    compile "io.reactivex.rxjava2:rxandroid:$rxLibVersion"
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile "io.reactivex.rxjava2:rxjava:$rxLibVersion"
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    compile 'co.trikita:knork:1.2.0'
    compile 'org.codejargon.feather:feather:1.0'
    compile 'com.github.satyan:sugar:1.4'
    compile 'org.greenrobot:eventbus:3.0.0'

    provided "org.projectlombok:lombok:1.16.10"
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

dexinfo {
    maxDepth 2
}
