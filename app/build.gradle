apply plugin: 'com.android.application'
apply plugin: 'com.mutualmobile.gradle.plugins.dexinfo'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.dariuszdeoniziak.charades"
        minSdkVersion 15
        targetSdkVersion 28 // API level 29 is not available for Robolectric
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner getInstrumentation()

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

def getInstrumentation() {
    project.hasProperty('cucumber') ? 'com.dariuszdeoniziak.charades.test.CucumberRunner' : 'androidx.test.runner.AndroidJUnitRunner'
}

def rxLibVersion = '2.1.1'
def roomLibVersion = '2.2.3'
def cucumberVersion = '4.3.1'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "org.robolectric:robolectric:4.3"

    androidTestImplementation 'org.mockito:mockito-android:2.23.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation "io.cucumber:cucumber-android:$cucumberVersion"
    androidTestImplementation "io.cucumber:cucumber-picocontainer:$cucumberVersion"
    implementation 'androidx.fragment:fragment-testing:1.2.1'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "com.google.android.material:material:1.1.0"

    implementation "androidx.room:room-runtime:$roomLibVersion"
    annotationProcessor "androidx.room:room-compiler:$roomLibVersion"
    testImplementation "androidx.room:room-testing:$roomLibVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxLibVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxLibVersion"

    implementation 'org.codejargon.feather:feather:1.0'
}

dexinfo { // To display run "./gradlew dexinfoDebug"
    maxDepth 2
}
